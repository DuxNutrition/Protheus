#INCLUDE "totvs.ch"
#INCLUDE "protheus.ch"

using namespace tlpp.regex

/*/{Protheus.doc} RemoveCEspecial
Remove character especial de campo de tabela informada
@type user function
@author TOTVS IP
@since 18/04/2024
@version 1.0
/*/
User Function RemoveCEspecial()
	Local cTab := "" as character
	Local cCampo := "" as character
	Local lOk := .t. as logical

	If Pergunte()
		cTab := Upper(AllTrim(MV_PAR01))
		cCampo := Upper(AllTrim(MV_PAR02))

		If FWTabPref(cCampo) == AllTrim(cTab)

			DbSelectArea("SX2")
			SX2->(DbSetOrder(1))
			If SX2->(DbSeek(cTab))
				lOk := .t.
			Else
				lOk := .f.
				MsgAlert("A tabela digitada não existe.", "Tabela não encontrada!")
			EndIf

			DbSelectArea(cTab)
			If &((cTab)+"->(FieldPos('"+cCampo+"'))")
				FWMsgRun(, {|| lOk := fProcessa(cTab, cCampo) },,"Aguarde, removendo characteres especiais...")
			Else
				lOk := .f.
				MsgAlert("O Campo digitado não existe.", "Campo não encontrado!")
			EndIf
		Else
			lOk := .f.
			MsgAlert("Tabela não corresponde ao campo informado.", "Atenção")
		EndIf
	EndIf

Return (NIL)

/*
** Processamento
*/
Static Function fProcessa(cTab, cCampo)
	Local cConteudo := "" as character
	Local cPermitido := AllTrim(GetNewPar("ZZ_PERMREG", ",-.\/()!;:{}=%#&_"))
	Local oRegex := Regex():new("[^\w\s"+cPermitido+"]")
	Local ltermina := .f.


	DbSelectArea(cTab)
	(cTab)->(DbSetOrder(1))
	(cTab)->(DbGoTop())
	While((cTab)->(!Eof()) )
		cConteudo := &(cTab + '->(' + cCampo + ')')
		lRet := oRegex:ReplaceAll(@cConteudo, "")

		If At(Char(170), cConteudo) > 0 .or. At(Char(186), cConteudo) > 0
			cConteudo := StrTran(cConteudo, Char(170), "")
			cConteudo := StrTran(cConteudo, Char(186), "")
			lRet := .t.
		EndIf

		If lRet
			(cTab)->(RecLock(cTab, .f.))
			&(cTab + '->(' + cCampo + ')') := cConteudo
			(cTab)->(MsUnlock())
		EndIf

		If ltermina
			exit
		EndIf
		(cTab)->(DbSkip())
	EndDo

Return

/*
** Parambox
*/
static function Pergunte()

	local aParamBox	:= {}
	local cPerg 	:= "ZZREMC01"

	AADD(aParamBox,{1,"Tabela"					, Space(3),"@!","","",""	, 20,.T.})	// mv_par01
	AADD(aParamBox,{1,"Campo"					, Space(10),"@!","","",""	, 40,.T.})	// mv_par02

return ParamBox(aParamBox,"Filtro para remoção de characteres especiais",,,,,,,,cPerg,.T.,.T.)
